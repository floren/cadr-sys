.c This file is part of the Lisp Machine manual.	-*-Bolio-*-
.c Defstruct.
.c This needs more work.  More examples are needed.
.c Everything needs more work....

.chapter Defstruct
.setq defstruct section-page
.setq defstruct-chapter chapter-number
.cindex structures
.cindex defstruct
.cindex record (structure)
.c Macro-defining macros!!!!!
.cindex macro defining macros

	3defstruct* provides a facility in Lisp for creating and
using aggregate datatypes with named elements.  These are like
structures in PL/I, or records in Pascal.  In the last two chapters we
saw how to use macros to extend the control structures of Lisp; here we
see how they can be used to extend Lisp's data structures as well.

.section Introduction to Structure Macros

	To explain the basic idea, assume you were writing a Lisp
program that dealt with space ships.  In your program, you want to
represent a space ship by a Lisp object of some kind.  The interesting
things about a space ship, as far as your program is concerned, are
its position (2x* and 2y*), velocity (2x* and 2y*), and mass.  How do you
represent a space ship?

	Well, the representation could be a list of the x-position,
2y* position, and so on.  Equally well it could be an array of five
elements, the zeroth being the 2x* position, the first being the
2y* position, and so on.  The problem with both of these representations
is that the ``elements'' (such as 2x* position) occupy places in the object
which are quite arbitrary, and hard to remember (Hmm, was the mass the
third or the fourth element of the array?).  This would make programs
harder to write and read.  It would not be obvious when reading
a program that an expression such as 3(cadddr ship1)* or 3(aref ship2 3)*
means ``the 2y* component of the ship's velocity'', and it would be very easy
to write 3caddr* in place of 3cadddr*.

What we would like to see are names, easy to remember and to understand.
If the symbol 3foo* were bound to a representation of a space ship, then
.lisp
(ship-x-position foo)
.end_lisp
could return its 2x* position, and
.lisp
(ship-y-position foo)
.end_lisp
its 2y* position, and so forth.  The 3defstruct* facility does just this.

	3defstruct* itself is a macro which defines a structure.  For the
space ship example above, we might define the structure by saying:
.lisp
(defstruct (ship)
  "Represents a space ship."
  ship-x-position
  ship-y-position
  ship-x-velocity
  ship-y-velocity
  ship-mass)
.end_lisp

This says that every 3ship* is an object with five named components.
(This is a very simple case of 3defstruct*; we will see the general form
later.)  The evaluation of this form does several things.  First, it
defines 3ship-x-position* to be a function which, given a ship, returns
the 2x* component of its position.  This is called an 2accessor function*,
because it 2accesses* a component of a structure.
'cindex accessor function
3defstruct* defines the other four accessor functions analogously.

3defstruct* also defines 3make-ship* to be a macro or function
(you can specify which one) that can create a 3ship* object.  So 3(setq s
(make-ship))* makes a new ship, and sets 3s* to it.  This is
called the 2constructor*, because it constructs a new structure.
'cindex constructor

We also want to be able to change the contents of a structure.  To do this,
we use the 3setf* macro (see (setf-fun)), as follows (for example):
.lisp
(setf (ship-x-position s) 100)
.end_lisp
Here 3s* is bound to a ship, and after the evaluation of the 3setf*
form, the 3ship-x-position* of that ship is 100.  Another way
to change the contents of a structure is to use the alterant macro,
which is described later, in (using-defstruct-alterant).

How does all this map into the familiar primitives of Lisp?  In this simple
example, we left the choice of implementation technique up to
3defstruct*; by default, it chooses to represent a ship as an array.  The array has
five elements, which are the five components of the ship.  The accessor
functions are defined thus:
.lisp
(defun ship-x-position (ship)
  (aref ship 0))
.end_lisp
The constructor form 3(make-ship)* performs 3(make-array 5)*, which
makes an array of the appropriate size to be a ship.  Note that a program which
uses ships need not contain any explicit knowledge that ships are represented
as five-element arrays; this is kept hidden by 3defstruct*.

The accessor functions are not actually ordinary functions; instead they
are substs (see (subst)).  This difference has two implications: it allows
3setf* to understand the accessor functions, and it allows the compiler to
substitute the body of an accessor function directly into any function that uses it,
making compiled programs that use 3defstruct* exactly equal in efficiency to
programs that ``do it by hand''.  Thus writing 3(ship-mass s)* is exactly
equivalent to writing 3(aref s 4)*, and writing 3(setf (ship-mass s) m)*
is exactly equivalent to writing 3(setf (aref s 4) m)*, when the program is compiled.
It is also possible to tell 3defstruct* to implement the accessor
functions as macros; this is not normally done in Zetalisp, however.

	We can now use the 3describe-defstruct* function to look at the
3ship* object, and see what its contents are:
.lisp
(describe-defstruct x 'ship) =>

#<art-q-5 17073131> is a ship
   ship-x-position:              100
   ship-y-position:              nil
   ship-x-velocity:              nil
   ship-y-velocity:              nil
   ship-mass:                    nil
#<art-q-5 17073131>
.end_lisp
(The 3describe-defstruct* function is explained more fully on
(describe-defstruct-fun).)

	By itself, this simple example provides a powerful structure
definition tool.  But, in fact, 3defstruct* has many other features.  First
of all, we might want to specify what kind of Lisp object to use for the
``implementation'' of the structure.  The example above implemented a ship
as an array, but 3defstruct* can also implement structures as array-leaders,
lists, and other things.  (For array-leaders, the accessor functions call
3array-leader*, for lists, 3nth*, and so on.)

	Most structures are implemented as arrays.  Lists take slightly less
storage, but elements near the end of a long list are slower to access.
Array leaders allow you to have a homogeneous aggregate (the array)
and a heterogeneous aggregate with named elements (the leader) tied
together into one object.  Packages are this sort of an object,
and so are the strings which Zmacs uses for storing lines of text.

	The constructor function or macro allows you to specify values
for slots in the new structure.  3defstruct* allows you to specify
default initial values for slots; whenever a structure is constructed
and no value is specified for a slot, the slot's default initial value
is stored in it.

	The 3defstruct* in Zetalisp also works in various
dialects of Maclisp, and so it has some features that are not useful in
Zetalisp.  When possible, the Maclisp-specific features attempt
to do something reasonable or harmless in Zetalisp, to make it
easier to write code that will run equally well in Zetalisp and
Maclisp.  (Note that this 3defstruct* is not necessarily the one installed in Maclisp!)

Note that there is another version of 3defstruct* used in Common Lisp
programs, which is slightly incompatible.  See (cli-defstruct).

.section How to Use Defstruct
.cindex slot

.defmac defstruct
A call to 3defstruct* looks like:
.lisp
(defstruct (2name* 2options*3...*)
  [2doc-string*]
  2slot-description-1*
  2slot-description-2*
  3...*)
.end_lisp
2name* must be a symbol; it is the name of the structure.  It is given a
3si:defstruct-description* property that describes the attributes and elements of the
structure; this is intended to be used by programs that examine Lisp
programs and that want to display the contents of structures in a helpful
way.  2name* is used for other things, described below.

Each 2option* may be either a symbol, which should be one of the recognized
option names listed in the next section, or a list, whose car should be one of the
option names and the rest of which should be arguments to the option.
Some options have arguments that default; others require that arguments be
given explicitly.

2doc-string* is a string which is recorded as the documentation of
2name* as a structure.  It can be accessed via 3(documentation
'2name* 'structure)*.  It is not required.

Each 2slot-description* may be in any of three forms:
.lisp
1(1)*	2slot-name*
1(2)*	(2slot-name* [2default-init* 2slot-options*3...*])
1(3)*	((2slot-name-1* 2byte-spec-1* [2default-init-1* 2slot-options*3...*])
	 (2slot-name-2* 2byte-spec-2* [2default-init-2* 2slot-options*3...*])
		3...*)
.end_lisp
Each 2slot-description* allocates one element of the physical structure,
even though in form (3) several slots are defined.

Each 2slot-name* must always be a symbol; an accessor function is defined
for each slot.

In form (1), 2slot-name* simply defines a slot with the given name.  An
accessor function is defined with the name 2slot-name* (but see the
3:conc-name* option, (defstruct-conc-name-option)).  Form (2) is
similar, but allows a default initialization for the slot.  Initialization
is explained further on (defstruct-initialization).  Form (3) lets you pack
several slots into a single element of the physical underlying structure, using
the byte field feature of 3defstruct*, which is explained on
(defstruct-byte-field).

Forms (2) and (3) allow 2slot-options* which are alternating
keywords and values (unevaluated).  These slot option keywords are
defined:
.table 3 0 1080
.item :read-only 2flag*
'kindex :read-only "defstruct slot option"
If 2flag* is non-3nil*, this specifies that this slot should not be changed in
an existing structure.  3setf* will not be allowed on the slot's
accessor.

.item :type 2type-spec*
'kindex :type "defstruct slot option"
Declares that the contents of this slot must be of type 2type-spec*.
The Lisp machine compiler does not use this information,
but sometimes it enables 3defstruct* to deduce that it
can pack the structure into less space by using a specialized array type.

.item :documentation 2documentation-string*
'kindex :documentation "defstruct slot option"
Makes 2documentation-string* the documentation for the slot's accessor
function.  It also goes in the 3si:defstruct-slot-description-documentation*
for this slot in the 3defstruct-description* structure.
.end_table

Here is an eggsample of using slot options:

.lisp
(defstruct (eggsample :named :conc-name) 
  (yolk 'a
   :documentation "First thing you need in an eggsample.") 
  (grade 3) 
  (albumen nil :read-only t)) 

(documentation 'eggsample-yolk 'function)
=> "First thing you need in an eggsample." 

(setf (eggsample-albumen (make-eggsample)) 'eggsistential)
>>ERROR: SETF is forbidden on EGGSAMPLE-ALBUMEN.
While in the function ...
.end_lisp
.end_defmac

Because evaluation of a 3defstruct* form causes many functions and macros
to be defined, you must take care not to define the same name with two
different 3defstruct* forms.  A name can only have one function
definition at a time; if it is redefined, the latest definition is the one
that takes effect, and the earlier definition is clobbered.  (This is no
different from the requirement that each 3defun* which is intended to
define a distinct function must have a distinct name.)

To systematize this necessary carefulness, as well as for
clarity in the code, it is conventional to prefix the names of all of the
accessor functions with some text unique to the structure.  In the example
above, all the names started with 3ship-*.  The 3:conc-name*
option can be used to provide such prefixes automatically (see (defstruct-conc-name-option)).
Similarly, the conventional name for the constructor in the example
above was 3make-ship*, and the conventional name for the alterant macro
(see (using-defstruct-alterant)) was 3alter-ship*.

The 3describe-defstruct* function lets you examine an instance of
a structure.

.defun describe-defstruct instance &optional name
3describe-defstruct* takes an 2instance* of a structure, and prints
out a description of the instance, including the contents of each of its
slots.  2name* should be the name of the structure; you must provide
the name of the structure so that 3describe-defstruct* can know what
structure 2instance* is an instance of, and therefore figure out what
the names of the slots of 2instance* are.

If 2instance* is a named structure, you don't have to provide 2name*,
since it is just the named structure symbol of 2instance*.
Normally the 3describe* function (see (describe-fun)) calls
3describe-defstruct* if it is asked to describe a named structure;
however some named structures have their own idea of how to describe themselves.
See (named-structure) for more information about named structures.
.end_defun

.section "Options to Defstruct"

This section explains each of the options that can be given to 3defstruct*.
Here is an example that shows the typical syntax of a call to 3defstruct*
that gives several options.
.lisp
(defstruct (foo (:type (:array (mod 256)))
		(:make-array (:leader-length 3))
		:conc-name
		(:size-macro foo))
  a b)
.end_lisp

.table 3
.item :type
'kindex :type defstruct
The 3:type* option specifies what kind of Lisp object to use
to implement the structure.  It must be given one argument, which must
be one of the symbols enumerated below, or a user-defined type.
If the option itself is not
provided, the type defaults to 3:array* in traditional programs,
or 3:vector* in Common Lisp programs.  You can define your
own types; this is explained in (defining-your-own-defstruct-types).

.table 3 0 500
.item :list
'kindex :list defstruct
Uses a list.
.item :named-list
'kindex :named-list defstruct
Like 3:list*, but the first element of the list holds the
symbol that is the name of the structure and so is not used
as a component.

.item :array
.item1 :typed-array
.item1 :vector
'kindex :array defstruct
'kindex :typed-array defstruct
'kindex :vector defstruct
These are all synonymous.
They use an array, storing components in the body of the array.
.item :named-array
'kindex :named-array defstruct
Like 3:array*, but makes the array a named structure (see
(named-structure)) using the name of the structure as the named
structure symbol.  Element 0 of the array holds the named
structure symbol and so is not used to hold a component of the
structure.
.item :named-typed-array
.item1 :named-vector
'kindex :named-typed-array defstruct
'kindex :named-vector defstruct
These two synonyms are
like 3:named-array* but the array always has a leader and the
named structure symbol is stored there.  As a result, it is
possible to use the 3:subtype* option to specify a restricted
array type, such as 3art-8b*.
.item :phony-named-vector
'kindex :phony-named-vector defstruct
This is what you get in Common Lisp if you say
3(:type :vector)* and 3:named*.

.item :array-leader
'kindex :array-leader defstruct
Use an array, storing components in the leader of the array.
(See the 3:make-array* option, described below.)
.item :named-array-leader
'kindex :named-array-leader defstruct
Like 3:array-leader*, but makes the array a named structure (see
(named-structure)) using the name of the structure as the named
structure symbol.  Element 1 of the leader holds the named
structure symbol and so is not used to hold a component of the
structure.

.item :fixnum-array
'kindex :fixnum-array defstruct
Like 3:array*, but the type of the array is 3art-32b*.
.item :flonum-array
'kindex :flonum-array defstruct
Like 3:array*, but the type of the array is 3art-float*.
.item :named-fixnum-array
.item1 :named-flonum-array
'kindex :named-fixnum-array defstruct
'kindex :named-flonum-array defstruct
Like 3:fixnum-array* or 3:flonum-array* but also a named structure, with a leader
to hold the named structure symbol.

.item :tree
'kindex :tree defstruct
The structure is implemented out of a binary tree of conses, with
the leaves serving as the slots.

.item :fixnum
'kindex :fixnum defstruct
This unusual type implements the structure as a single fixnum.  The
structure may only have one slot.  This is only useful with the
byte field feature (see (defstruct-byte-field)); it lets you store
a bunch of small numbers within fields of a fixnum, giving the
fields names.

.item :grouped-array
'kindex :grouped-array defstruct
This is described in (grouped-array).
.end_table

The argument of 3:type* may also have the form
3(2type* 2subtype*)*.  This is equivalent to
specifying 2type* for the 3:type* option and 2subtype*
for the 3:subtype* option.  For example, 3(:type (:array (mod 16.)))*
specifies an array of four-bit bytes.

.item :subtype
For structures which are arrays, 3:subtype* permits the array type
to be specified.  It requires an argument, which must be either
an array type name such as 3art-4b* or a type specifier restricting the elements
of the array.  In other words, it should be a suitable value for either
the 2type* or the 2element-type* argument to 3make-array*.

If no 3:subtype* option is specified but a 3:type* slot option is
given for every slot, 3defstruct* may deduce a subtype automatically
to make the structure more compact.

See (type-specifiers) for more information on type specifiers.

.item :constructor
'kindex :constructor defstruct
Specifies how to make a constructor for the structure.  In the simplest
use, there is one argument, which specifies the name to give to the
standard keyword-argument constructor.  If the argument is not provided
or if the option itself is not provided, the name of the constructor is
made by concatenating the string 3"make-"* to the name of the
structure.  If the argument is provided and is 3nil*, no constructor
is defined.  More complicated usage is explained in
(using-defstruct-constructor).

.item :alterant
'kindex :alterant defstruct
Takes one argument, which specifies the name of the alterant
macro.  If the argument is not provided, the name of the alterant is
made by concatenating the string 3"alter-"* to the name of the
structure.  If the argument is provided and is 3nil*, no alterant is
defined.  Use of the alterant macro is explained in
(using-defstruct-alterant).

In Common Lisp programs, the default for 3:alterant* is 3nil*;
no alterant is defined.  In traditional programs, the default is 3alter-2name**.

.xitem :predicate defstruct
'setq defstruct-predicates page
Causes 3defstruct* to generate a predicate
to recognize instances of the structure.  Naturally it only works for
``named'' types.  The argument to the 3:predicate* option is the name of
the predicate.  If the option is present without an argument, then the
name is formed by concatenating `3-p*' to the end of the name symbol
of the structure.  If the option is not present, then no predicate is
generated.  Example:
.lisp
(defstruct (foo :named :predicate)
  a
  b)
.end_lisp
defines a single argument function, 3foo-p*, that is true only of
instances of this structure.

The defaulting of the 3:predicate* option is different (and complicated)
in Common Lisp programs.  See (cli-defstruct).

.xitem :copier defstruct
Causes 3defstruct* to generate a single argument function that can
copy instances of this structure.  Its argument is the name of the copying
function.  If the option is present without an argument, then the name is
formed by concatenating `3copy-*' with the name of the structure.  Example:
.lisp
(defstruct (foo (:type :list) :copier)
  foo-a
  foo-b)
.end_lisp
Generates a function approximately like:
.lisp
(defun copy-foo (x)
  (list (car x) (cadr x)))
.end_lisp

.item :default-pointer
'kindex :default-pointer defstruct
'setq defstruct-default-pointer-option page
Normally, the accessors defined by 3defstruct* expect to
be given exactly one argument.  However, if the 3:default-pointer*
argument is used, the argument to each accessor is optional.  If
the accessor is used with no argument, it evaluates the default-pointer
form to find a structure and accesses the appropriate component of
that structure.  Here is an example:
.lisp
(defstruct (room 
	     (:default-pointer *default-room*))
   room-name
   room-contents)

(room-name x) ==> (aref x 0)
(room-name)   ==> (aref *default-room* 0)
.end_lisp

If the argument to the 3:default-pointer* argument is not given, it
defaults to the name of the structure.

.item :conc-name
'kindex :conc-name defstruct
'setq defstruct-conc-name-option page
It is conventional to begin the names of all the accessor functions of
a structure with a specific prefix, usually
the name of the structure followed by a hyphen.  The 3:conc-name*
option allows you to specify this prefix and have it concatenated
onto the front of all the slot names to make the names of the accessor
functions.  The argument should be a string to be used as the prefix,
or a symbol whose pname is to be used.
If 3:conc-name* is specified without an argument, the prefix is the
name of the structure followed by a hyphen.  If the argument is 3nil* or 3""*,
the names of the accessors are the same as the slot names,
and it is up to you to name the slots according to some suitable convention.

In Common Lisp programs, the default for 3:conc-name*, when this
option is not specified,  is the structure name followed by a hyphen.
For traditional programs, the default is 3nil*.

The keywords recognized by the constructor and alterant are the slot
names, not the accessor names, transfered into the keyword package.  It
is important to keep this in mind when using 3:conc-name*, since it
causes the slot and accessor names to be different.  Here is an example:

.lisp
(defstruct (door :conc-name)
   knob-color
   width)

(setq d (make-door :knob-color 'red :width 5.0))

(door-knob-color d) ==> red
.end_lisp

.item :include
'kindex :include defstruct
'setq defstruct-include-option page
Builds a new structure definition as an extension of an old
structure definition.  Suppose you have a structure called 3person* that
looks like this:

.lisp
(defstruct (person :named :conc-name)
   name
   age
   sex)
.end_lisp

Now suppose you want to make a new structure to represent an astronaut.
Since astronauts are people too, you would like them to also have the
attributes of name, age, and sex, and you would like Lisp functions
that operate on 3person* structures to operate just as well on
3astronaut* structures.  You can do this by defining 3astronaut*
with the 3:include* option, as follows:
.lisp
(defstruct (astronaut :named (:include person) 
		      :conc-name)
   helmet-size
   (favorite-beverage 'tang))
.end_lisp

The argument to the 3:include* option is required, and must be the
name of some previously defined structure of the same type as this
structure.  3:include* does not work with structures of type
3:tree* or of type 3:grouped-array*.

The 3:include* option inserts the slots of the included structure at
the front of the list of slots for this structure.  That is, an
3astronaut* has five slots; first the three defined in 3person*, and
then after those the two defined in 3astronaut* itself.  The accessor
functions defined by the 3person* structure, such as 3person-name*, can be used also on
3astronaut*'s.  New accessor functions are generated for these slots
in the 3astronaut* structure as if they were defined afresh;
their names start with 3astronaut-* instead of 3person-*.
In fact, the functions 3person-age* and 3astronaut-age* receive
identical definitions.

Since the structures are named structures, recognizable by 3typep*,
3subtypep* considers 3astronaut* a subtype of 3person*,
and 3typep* considers any 3astronaut* to be of type 3person*.

The following examples illustrate how you can
construct and use 3astronaut* structures:

.lisp
(setq x (make-astronaut :name 'buzz
			:age 45.
			:sex t
			:helmet-size 17.5))

(person-name x) => buzz
(astronaut-name x) => buzz
(astronaut-favorite-beverage x) => tang

(typep x 'astronaut) => t
(typep x 'person) => t
.end_lisp

Note that the 3:conc-name* option was 2not* inherited from the
included structure; it is present for 3:astronaut* only because it was
specified explicitly in the definition.  Similarly, the
3:default-pointer* and 3:but-first* options are not inherited from
the 3:include*'d structure.

The following is an advanced feature.  Sometimes, when one structure
includes another, the default values or slot options for the slots that
came from the included structure are not what you want.  The new
structure can specify new default values or slot options for the
included slots by giving the 3:include* option as:

.lisp
(:include 2name new-descriptor-1 3...* new-descriptor-n*)
.end_lisp

Each 2new-slot-descriptor* is just like the slot descriptors used for
defining new slots, except that byte fields are not allowed.  The
default initialization specified in 2new-slot-descriptor*, or the
absence of one, overrides what was specified in the included structure
type (3person*).  Any slot option values specified in 2new-slot-descriptor*
also override the values given in the included structure's definition.
Any inherited slots for which no 2new-slot-descriptor* is given,
and any slot options not explicitly overridden, are inherited.

For example, if we had wanted to define 3astronaut* so that the
default age for an astronaut is 345.*, and provide
documentation for its accessor, then we could have said:
.lisp
(defstruct (astronaut :conc-name
	     (:include person 
	       (age 45. :documentation
	         "The ASTRONAUT's age in years.")))
   helmet-size
   (favorite-beverage 'tang))
.end_lisp

If the 3:read-only* option is specified as 3nil* when 3t*
would have been inherited, an error is signaled.

.item :named
'kindex :named defstruct
This means that you want to use one of the ``named'' types.  If you
specify a type of 3:array*, 3:array-leader*, or 3:list*, and give
the 3:named* option, then the 3:named-array*,
3:named-array-leader*, or 3:named-list* type is used instead.
Asking for type 3:array* and giving the 3:named* option as well
is the same as asking for the type 3:named-array*; the only difference
is stylistic.

The 3:named* option works quite differently in Common Lisp programs;
see (cli-defstruct).

.item :make-array
'kindex :make-array defstruct
'setq defstruct-make-array-option page
If the structure being defined is implemented as an array, this option
may be used to control those aspects of the array that are not otherwise constrained by
3defstruct*.  For example, you might want to control the area in which
the array is allocated.  Also, if you are creating a structure of type
3:array-leader*, you almost certainly want to specify the dimensions
of the array to be created, and you may want to specify the type of the
array.

The argument to the 3:make-array* option should be a list of alternating
keyword symbols for the 3make-array* function (see (make-array-fun)),
and forms whose values are the arguments to those keywords.  For example,
3(:make-array (:area 'permanent-storage-area))* would request that the array
be allocated in a particular area.  Note that the keyword symbol is 2not* evaluated.

3defstruct* overrides any of the 3:make-array* options that it needs
to.  For example, if your structure is of type 3:array*, then
3defstruct* supplies the size of that array regardless of what you
say in the 3:make-array* option.  If you use the 3:initial-element*
3make-array* option, it initializes all the slots, but
3defstruct*'s own initializations are done afterward.  If a subtype
has been specified to or deduced by 3defstruct*, it overrides
any 3:type* keyword in the 3:make-array* argument.

Constructors for structures implemented as arrays recognize the keyword
argument 3:make-array*.  Attributes supplied therein overide any
3:make-array* option attributes supplied in the original 3defstruct*
form.  If some attribute appears in neither the invocation of the
constructor nor in the 3:make-array* option to 3defstruct*, then the
constructor chooses appropriate defaults.  The 3:make-array* option
may only be used with the default style of
constructor that takes keyword arguments.

If a structure is of type 3:array-leader*, you probably want to
specify the dimensions of the array.  The dimensions of an array are
given to 3:make-array* as a position argument rather than a keyword
argument, so there is no way to specify them in the above syntax.  To
solve this problem, you can use the keyword 3:dimensions* or the
keyword 3:length* (they mean the same thing), with a value that is
anything acceptable as 3make-array*'s first argument.

.item :times
'kindex :times defstruct
Used for structures of type 3:grouped-array* to control
the number of repetitions of the structure to be allocated by the
constructor.  (See (grouped-array).)  The constructor also accepts a
keyword argument 3:times* to override the value given in the
3defstruct*.  If 3:times* appears in neither the invocation of the
constructor nor as a 3defstruct* option, the constructor allocates
only one instance of the structure.

.item :size-macro
'kindex :size-macro defstruct
'setq defstruct-size-macro-option page
Defines a special macro to expand into the size of this structure.  The
exact meaning of the size varies, but in general this number is the one
you would need to know if you were going to allocate one of these
structures yourself (for example, the length of the array or list).  The
argument of the 3:size-macro* option is the name to be used for the
macro.  If this option is present without an argument, then the name of
the structure is concatenated with `3-size*' to produce the macro
name.

Example:
.lisp
(defstruct (foo :conc-name :size-macro)
  a b)
(macroexpand '(foo-size)) => 2
.end_lisp

.item :size-symbol
'kindex :size-symbol defstruct
Like 3:size-macro* but defines a global variable rather than a macro.
The size of the structure is the variable's value.  Use of 3:size-macro*
is considered cleaner.

.item :initial-offset
'kindex :initial-offset defstruct
This allows you to tell 3defstruct* to skip over a certain
number of slots before it starts allocating the slots described in the
body.  This option requires an argument (which must be a fixnum), which
is the number of slots you want 3defstruct* to skip.  To make use of
this option requires that you have some familiarity with how 3defstruct*
is implementing your structure; otherwise, you will be unable to make
use of the slots that 3defstruct* has left unused.

.item :but-first
'kindex :but-first defstruct
This option is best explained by example:

.lisp
(defstruct (head (:type :list)
		 (:default-pointer person)
		 (:but-first person-head))
  nose
  mouth
  eyes)
.end_lisp

The accessors expand like this:

.lisp
(nose x)	==> (car (person-head x))
(nose)		==> (car (person-head person))
.end_lisp

The idea is that 3:but-first*'s argument is an accessor from some
other structure, and it is never expected that this structure will be
found outside of that slot of that other structure.  Actually, you can
use any one-argument function, or a macro that acts like a one-argument
function.  It is an error for
the 3:but-first* option to be used without an argument.

.xitem :callable-accessors defstruct
Controls whether accessors are really functions,
and therefore ``callable'', or whether they are really macros.  With
an argument of 3t*, or with no argument, or if the option is not
provided, then the accessors are really functions.  Specifically,
they are substs, so that they have all the efficiency of macros
in compiled programs, while still being function objects that
can be manipulated (passed to 3mapcar*, etc.).  If
the argument is 3nil* then the accessors are really macros.

.xitem :callable-constructors defstruct
Controls whether constructors are really functions,
and therefore ``callable'', or macros.  An argument of 3t*
makes them functions; 3nil* makes them macros.
The default is 3t* in Common Lisp programs, 3nil* in
traditional programs.  See (using-defstruct-constructor) for more information.

.xitem :property defstruct
'setq defstruct-property-option page
For each structure defined by defstruct, a property list is maintained
for the recording of arbitrary properties about that structure.  (That
is, there is one property list per structure definition, not one for
each instantiation of the structure.)

The 3:property* option can be used to give a 3defstruct* an
arbitrary property.  3(:property 2property-name value*)* gives the
3defstruct* a 2property-name* property of 2value*.  Neither
argument is evaluated.  To access the property list, the user must
look inside the 3defstruct-description* structure himself (see
(defstruct-description)).

.xitem :print defstruct
Controls the printed representation of his structure in a way
independent of the Lisp dialect in use.  Here is an example:
.lisp
(defstruct (foo :named
	        (:print "#<Foo ~S ~S>"
                        (foo-a foo) (foo-b foo)))
  foo-a
  foo-b)
.end_lisp

Of course, this only works if you use some named type, so that the system
can recognize examples of this structure automatically.

The arguments to the 3:print* option are arguments to the 3format*
function (except for the stream of course!).  They are evaluated in an
environment where the name symbol of the structure (3foo* in this
case) is bound to the instance of the structure to be printed.

This works by generating a 3defselect* that creates a named structure
handler.  Do not use the 3:print* option if you define a named
structure handler yourself, as they will conflict.

.xitem :print-function
is the Common Lisp version of the 3:print* option.  Its argument is a function
to print a structure of this type, and it is called with three arguments:
the structure to be printed, the stream to print it on, and the current printing depth
(which should be compared with 3*print-level** to decide when to cut off
recursion and print `3#*').
The function is expected to observe the values of the various printer-control variables
such as 3*print-escape** (see (*print-escape*-var)).  Example:

.lisp
(defstruct (bar :named :conc-name
                (:print-function
                  (lambda (struct stream depth)
                    depth ;1 unused*
                    (sys:printing-random-object
			(struct stream :type)
                      (format stream "with zap ~S"
                              (bar-zap struct))))))
  "The famous BAR structure."
  (zap 'yow)
  random-slot)

(make-bar) => #<BAR with zap YOW>
.end_lisp

.item 2type*
In addition to the options listed above, any currently defined type
(any legal argument to the 3:type* option) can be used as an option.
This is mostly for compatibility with the old version of 3defstruct*.
It allows you to say just 2type* instead of 3(:type 2type*)*.  It
is an error to give an argument to one of these options.

.item 2other*
Finally, if an option isn't found among those listed above,
it should be a valid defstruct-keyword for the type
of structure being defined, and the option should be of the form
3(2option-name value*)*.  If so, the option is treated
just like 3(:property 2option-name value*)*.
That is, the 3defstruct* is given an 2option-name* property of 2value*.

This provides a primitive way for you to define your own options to
3defstruct*, particularly in connection with user-defined types (see
(defining-your-own-defstruct-types)).  Several of the options listed above
are actually implemented using this mechanism.  They include
3:times*, 3:subtype* and 3:make-array*.

The valid defstruct-keywords of a type are in a list in the 
3defstruct-keywords* slot of the
3defstruct-type-description* structure for 2type*.
.end_table

.section Using the Constructor and Alterant

After you have defined a new structure with 3defstruct*, you can
create instances of this structure using the constructor, and
you can alter the values of its slots using the alterant macro.  By
default, traditional 3defstruct* defines both the constructor and the alterant,
forming their names by concatenating `3make-*' and `3alter-*',
respectively, onto the name of the structure.  The 3defstruct* for Common Lisp 
programs defines no alterant by default.  You can specify the names
yourself by passing the name you want to use as the argument to the
3:constructor* or 3:alterant* options, or specify that you don't
want the macro created at all by passing 3nil* as the argument.

.subsection Constructors
.setq using-defstruct-constructor section-page
.cindex constructor

A call to a constructor, in general, has the form
.lisp
(2name-of-constructor*  2keyword-1* 2value-1*  2keyword-2* 2value-2*  3...*)
.end_lisp
Each 2keyword* is a keyword (a symbol in the 3keyword* package)
whose name matches one of the slots of the structure,
or one of a few specially recognized keywords.

The name of the constructor is specified by the 3:constructor* option,
which can also specify a documentation string for it:
.lisp
(:constructor 2name-of-constructor* [2doc-string*])
.end_lisp

'setq defstruct-initialization page
If a 2keyword* matches the name of a slot (2not* the name of an
accessor), then the corresponding 2value* is used to initialize that
slot of the new structure.  Any slots whose values are not specified in
this way are initialized to the values of the default initial value forms
specified in the 3defstruct*.  If no default initial value was specified
either for a slot, that slot's initial value is undefined.
You should always specify the initialization, either in the 3defstruct*
or in the constructor invocation, if you care about the initial value of the slot.

Constructors may be macros or functions.  They are functions
if the 3:callable-constructors* option to 3defstruct*
is non-3nil*.  By default, they are functions in Common Lisp programs
and macros in traditional programs.

Constructor macros allow the slot name (in its own package) to be
used instead of a keyword.  Constructor functions do not, as they
are ordinary functions defined using 3&key*.  Old code using slot
names not in the keyword package should be converted.

The default initial value forms are evaluated (if needed) each
time a structure is constructed, so that if 3(gensym)* is used as a
default initial value form then a new symbol is generated for each
structure.  The order of evaluation of the default initial value forms
is unpredictable.  When the constructor is a macro, the order of
evaluation of the keyword argument forms it is given is also
unpredictable.

The two special keyword arguments recognized by constructors are 3:make-array*
and 3:times*.  3:make-array* should be used only for structures
which are represented as arrays, and 3:times* only for 3:grouped-array*
structures.  If one of these arguments is given, then it overrides the 3:make-array*
option or the 3:times* option (see (defstruct-make-array-option))
specified in the 3defstruct*.  For example:
.lisp
.exdent 96 For example,
'c with a constructor macro
(make-ship :ship-x-position 10.0
           :ship-y-position 12.0
           :make-array '(:leader-length 5 :area disaster-area))
'c .exdent 96 or with a constructor function
'c (make-ship :ship-x-position 10.0
'c            :ship-y-position 12.0
'c            :make-array '(:leader-length 5 :area disaster-area))
.end_lisp

'c Note that the argument of a special keyword is evaluated if the
'c constructor is a function, but not if it is a macro.  This
'c incompatibility is unfortunately unavoidable.

User-defined types of structures can define their own special
constructor keywords.

.subsection By-Position Constructors

If the 3:constructor* option is given as
3(:constructor 2name* 2arglist* [2doc-string*])*, then instead of making a keyword
driven constructor, 3defstruct* defines a positional constructor,
taking arguments whose meaning is determined by the argument's position
rather than by a keyword.
The 2arglist* is used to describe what arguments the constructor should accept.
In the simplest case something like
3(:constructor make-foo (a b c))* defines 3make-foo* to be a three-argument
constructor macro whose arguments are used to initialize the
slots named 3a*, 3b*, and 3c*.

In addition, the keywords 3&optional*, 3&rest*, and 3&aux* are
recognized in the argument list.  They work in the way you might expect,
but there are a few fine points worthy of explanation:

.lisp
(:constructor make-foo 
	(a &optional b (c 'sea) &rest d &aux e (f 'eff))
  "Make a FOO, with positional arguments")
.end_lisp

This defines 3make-foo* to be a constructor of one or more arguments.
The first argument is used to initialize the 3a* slot.  The second
argument is used to initialize the 3b* slot.  If there isn't any
second argument, then the default value given in the body of the
3defstruct* (if given) is used instead.  The third argument is used to
initialize the 3c* slot.  If there isn't any third argument, then the
symbol 3sea* is used instead.  Any arguments following the third
argument are collected into a list and used to initialize the 3d*
slot.  If there are three or fewer arguments, then 3nil* is placed in
the 3d* slot.  The 3e* slot 2is not initialized*; its initial
value is undefined, even if a default value was specified in its slot-description.
Finally, the 3f* slot is initialized to contain
the symbol 3eff*.

	The actions taken in the 3b* and 3e* cases were carefully
chosen to allow the user to specify all possible behaviors.  Note that
the aux ``variables'' can be used to override completely the default
initializations given in the body.

	Since there is so much freedom in defining constructors this
way, it would be cruel to only allow the 3:constructor* option to be
given once.  So, by special dispensation, you are allowed to give the
3:constructor* option more than once, so that you can define several
different constructors, each with a different syntax.  These
may include both keyword and positional constructors.  If there are
multiple keyword constructors, they all behave the same, differing
only in the name.  3It is important
to have a keyword constructor* because otherwise the 3#S* reader
construct cannot work.

	Note that positional constructors may be macros or functions,
just like keyword constructors, and based on the same criterion: they
are functions if the 3:callable-constructors* option to 3defstruct*
is non-3nil*.  By default, they are functions in Common Lisp programs
and macros in traditional programs.  If the positional constructor is a
macro, then the actual order of evaluation of its arguments is
unpredictable.

	  Also note that you cannot specify the 3:make-array*
or 3:times* information in a positional constructor.

.subsection Alterant Macros
.setq using-defstruct-alterant section-page
.cindex alterant macro

A call to the alterant macro, in general, has the form
.lisp
(2name-of-alterant-macro* 2instance-form* 
    2slot-name-1* 2form-1*
    2slot-name-2* 2form-2*
    2...*)
.end_lisp
2instance-form* is evaluated and should return an instance of the
structure.  Each 2form* is evaluated and the corresponding slot is
changed to have the result as its new value.  The slots are altered
after all the 2forms* are evaluated, so you can exchange the values
of two slots, as follows:
.lisp
(alter-ship enterprise
    :ship-x-position (ship-y-position enterprise)
    :ship-y-position (ship-x-position enterprise))
.end_lisp

As with constructor macros, the order of evaluation of the 2forms* is
undefined.  Using the alterant macro can produce more efficient Lisp
than using consecutive 3setf*s when you are altering two byte fields
of the same object, or when you are using the 3:but-first* option.

.section Byte Fields
'setq defstruct-byte-field page
	The byte field feature of 3defstruct* allows you to specify that
several slots of your structure are bytes (see
(byte-manipulation-functions)) in an integer stored in one
element of the structure.  For example, suppose we had the following
structure:
.lisp
(defstruct (phone-book-entry (:type :list))
  name
  address
  (area-code 617.)
  exchange
  line-number)
.end_lisp

This works correctly but it wastes space.  Area codes and
exchange numbers are always less than 1000, and so both can fit
into 10 bit fields when expressed as binary numbers.  Since Lisp
Machine fixnums have (more than) 20 bits, both of these
values can be packed into a single fixnum.  To tell 3defstruct* to do
so, you can change the structure definition to the following:

.lisp
(defstruct (phone-book-entry (:type :list))
  name
  address
  ((area-code (byte 10. 10.) 617.)
   (exchange (byte 10. 0)))
  line-number)
.end_lisp

The expressions 3(byte ...)* calculate byte specifiers to be used
with the functions 3ldb* and 3dpb*.  The accessors, constructor, and
alterant will now operate as follows:

.lisp
(area-code pbe)	==> (ldb (byte 10. 10.) (caddr pbe))
(exchange pbe)	==> (ldb (byte 10. 0) (caddr pbe))
.end_lisp
.lisp

(make-phone-book-entry
   :name "Fred Derf"
   :address "259 Octal St."
   :exchange ex
   :line-number 7788.)

==> (list "Fred Derf" "259 Octal St." 
          (dpb ex (byte 10. 0) 631808.)
	  7788.)

.end_lisp
.lisp
(alter-phone-book-entry pbe
   :area-code ac
   :exchange ex)

==> ((lambda (#:g0530)
       (setf (nth 2 #:g0530)
	     (dpb ac (byte 10. 10.) 
		     (dpb ex (byte 10. 0) (nth 2 #:g0530)))))
     pbe)
.end_lisp

(This is how the expression would print; this text would not read in
properly because a new uninterned symbol would be created by each use of
7#:*.)

Note that the alterant macro is optimized to only read and write the
second element of the list once, even though you are altering two
different byte fields within it.  This is more efficient than using two
3setf*'s.  Additional optimization by the alterant macro occurs if the
byte specifiers in the 3defstruct* slot descriptions are constants.
However, you don't need to worry about the details of how the alterant
macro does its work.

If the byte specifier is 3nil*, then the accessor is
defined to be the usual kind that accesses the entire Lisp object,
thus returning all the byte field components as a fixnum.  These slots
may have default initialization forms.

The byte specifier need not be a constant; a variable or, indeed, any
Lisp form, is legal as a byte specifier.  It is evaluated each
time the slot is accessed.  Of course, unless you are doing something
very strange you will not want the byte specifier to change between
accesses.

Constructors (both functions and macros) initialize
words divided into byte fields as if they were deposited in in the
following order:

 1) Initializations for the entire word given in the defstruct form.

 2) Initializations for the byte fields given in the defstruct form.

 3) Initializations for the entire word given in the constructor invocation.

 4) Initializations for the byte fields given in the constructor invocation.

.c should have example of how this works -- using phone-book-entry, for example.

Alterant macros work similarly: the modification for the entire Lisp
object is done first, followed by modifications to specific byte fields.
If any byte fields being initialized or altered overlap each other, the
action of the constructor and alterant is unpredictable.

.section Grouped Arrays
.setq grouped-array section-page
.cindex grouped array

	The grouped array feature allows you to store several instances
of a structure side-by-side within an array.  This feature is somewhat
limited; it does not support the 3:include* and 3:named* options.

	The accessor functions are defined to take an extra argument, which
should be an integer, and is the index into the array of where this
instance of the structure starts.  This index should normally be a multiple
of the size of the structure, for things to make sense.  Note that the
index is the 2first* argument to the accessor function and the structure
is the 2second* argument, the opposite of what you might expect.  This
is because the structure is 3&optional* if the 3:default-pointer* option
is used.

	Note that the ``size'' of the structure (for purposes of the
3:size-symbol* and 3:size-macro* options) is the number of elements
in 2one* instance of the structure; the actual length of the array is
the product of the size of the structure and the number of instances.
The number of instances to be created by the constructor is taken from
the 3:times* keyword of the constructor or the argument to the
3:times* option to 3defstruct*.

.section Named Structures

'setq named-structure page
.cindex named structure

	The 2named structure* feature provides a very simple form of
user-defined data type.  (Flavors are another, more powerful, facility
for defining data types, but they are more expensive in simple cases.  See
(flavor).)  A named structure is actually an array, containing
elements and optionally a leader.  The difference between a named
structure and an ordinary array is that the named structure also contains
an explicit slot to hold its ostensible data type.  This data type is a
symbol, any symbol the programmer cares to use.  In traditional
programs, named structures are normally defined using 3defstruct* with
the 3:named* option.  In Common Lisp programs, 3defstruct* defines a
named structure by default.  Individual named structures are made with
the constructors defined by 3defstruct*.

The data type symbol of a named structure is also called the 2named
structure symbol*.  It is stored in array element 0 if the structure
has no leader.  If there is a leader, the type symbol is stored in array
leader element 1 (recall that element 0 is reserved for the fill
pointer).  If a numeric-type array is to be a named structure, it must
have a leader, since a symbol cannot be an element of a numeric array.

Named structure are 2recognizable*; that is, if you define a named
structure called 3foo*, you can always tell whether an object is a
3foo* structure.  No array created in the normal fashion, no matter
what components it has, will be mistaken for a genuine 3foo*.

Named structures can recognized by 3typep*.  Specify 3foo*,
the named structure name, as the second argument, and the object to be
tested as the first argument.  3type-of* of an ordinary array returns
3array*, but 3type-of* of a named structure returns the explicitly
recorded data type symbol.

.lisp
(defstruct (foo :named) a b)
(type-of (make-foo)) => foo
(typep (make-foo) 'foo) => t
.end_lisp

Named structures of other types which include 3foo* are also recognized
as 3foo*'s by 3typep*.  For example, using the previously-given definitions
of 3person* and 3astronaut*, then
.lisp
(typep (make-astronaut) 'person) => t
.end_lisp
because the type 3person* was explicitly included by the 3defstruct*
for 3astronaut*.  Indirect includes count also:

.lisp
(defstruct (mission-specialist :named
	     (:include astronaut))
   3...*)

(typep (make-mission-specialist) 'person) => t
(subtypep 'person 'mission-specialist) => t
.end_lisp

It should be emphasized that the named structure 2is* an array.
All the usual array functions, such as 3aref* and 3array-dimension*,
can be used on it.  If it is one-dimensional (as is usually the case)
then the named structure is a vector and the generic sequence functions
can be used on it.

.lisp
(typep (make-astronaut) 'array) => t
(arrayp (make-astronaut)) => t
(array-rank (make-astronaut)) => 1
.end_lisp

Because named structure data types are recognizable, they can define generic
operations and say how to handle them.  A few
such operations are defined by the system and are invoked automatically
from well-defined places.  For example 3print* automatically invokes
the 3:print-self* operation if you give it a named structure.  Thus,
each type of named structure can define how it should print.  The standardly
defined named structure operations are listed below.  You can also define
new named structure operations and invoke them by calling the named structure
as a function just as you would invoke a flavor instance.

Operations on a named structure are all handled by a single function,
which is found as the 3named-structure-invoke* property of the
structure type symbol.  It is OK for a named structure type to have no
handler function.  Then invocation of any operation on the named
structure returns 3nil*, and system routines such as 3print* take
default actions.

If a handler function exists, it is given these arguments:
.table 2
.item operation
The name of the operation being invoked; usually a keyword.
.item structure
The named structure which is being operated on.
.item additional-arguments...
Any other arguments which were passed when the operation was invoked.
The handler function should have a rest parameter so it can accept
any number of arguments.
.end_table

.nopara
The handler function should return 3nil* if it does not recognize
the 2operation*.
These are the named structure operations used by the system at present:
.table 3
.item :which-operations
Should return a list of the names of the operations the function handles.
Every handler function must handle this operation, and every opertation
that the function handles should be included in this list.

.item :print-self
Should output the printed representation of the named structure to a stream.
The additional arguments are the stream to output to, the current
depth in list-structure, and the current value of 3*print-escape**.  If
3:print-self* is not in the value returned by 3:which-operations*,
or if there is no handler function, 3print* uses 3#s* syntax.

.item :describe
'setq named-structure-describe-method page
Is invoked by 3describe* and should output a description of the
structure to 3*standard-output**.  If there is no handler function
or 3:describe* is not in its 3:which-operations* list,
3describe* prints the names and values of the structure's fields as
defined in the 3defstruct*.

.item :sxhash
Is invoked by 3sxhash* and should return a hash code to
use as the value of 3sxhash* for this structure.  It is often useful
to call 3sxhash* on some (perhaps all) of the components of the
structure and combine the results in some way.

There is one additional argument to this operation: a flag saying
whether it is permissible to use the structure's address in forming the
hash code.  For some kinds of structure, there may be no way to generate
a good hash code except to use the address.  If the flag is 3nil*,
they must simply do the best they can, even if it means always returning
zero.

It is permissible to return 3nil* for 3:sxhash*.  Then 3sxhash*
produces a hash code in its default fashion.

.item :fasd-fixup
Is invoked by 3fasload* on a named structure that has
been created from data in a QFASL file.  The purpose of the operation
is to give the structure a chance to ``clean itself up'' if, in order to
be valid, it needs to have contents that are not exactly identical
to those that were dumped.  For example, readtables push themselves onto
the list 3si:*all-readtables** so that they can be found by name.

For most kinds of structures it is
acceptable not to define this operation at all (so that it returns 3nil*).
.end_table

Example handler function:
.lisp
(defun (:property person named-structure-invoke)
       (op self &rest args)
  (selectq op
    (:which-operations '(:print-self :describe))
    (:describe
      (format (car args)
	      "This is a ~D-year-old person"
	      (person-age self)))
    (:print-self
      (if *print-escape*
	  (si:printing-random-object (self (car args) :type)
	    (princ (person-name self) (car args)))
	(princ (person-name self) (car args))))))
.exdent 96 or
(defselect ((:property person named-structure-handler)
	    ignore)
  (:print-self (self stream ignore &optional ignore)
    (if *print-escape*
	(si:printing-random-object (self stream :type)
	  (princ (person-name self) stream))
      (princ (person-name self) stream)))
  (:describe (self)
    (format *standard-output*
	    "This is a ~D-year-old person"
	    (person-age self))))
.end_lisp

.nopara
This handler causes a person structure to include its name in its printed
representation; it also causes 3princ* of a person to print just the
name, with no `3#<*' syntax.  This simple example could have been
done even more simply with the 3:print-function* option.

It is often convenient to define a handler function with 3defselect*;
but you must be careful.  3defselect* by default defines the function
to signal an error if it is called with a first argument that is not
recognized.  A handler function should return 3nil* and get no error.
To avoid the problem, specify 3ignore* as the default handler when you
write the 3defselect*.  See (defselect-fun).

Note that the handler function of a named structure type is 2not*
inherited by other named structure types that include it.  For
example, the above definition of a handler for 3person* has no
effect at all on the 3astronaut* structure.  If you need such
inheritance, you must use flavors rather than named structures (see
(flavor)).

.nopara
The following functions operate on named structures.

.defun named-structure-p x
This semi-predicate returns 3nil* if 2x* is not a named structure; otherwise
it returns 2x*'s named structure symbol.
.end_defun

.defun make-array-into-named-structure array
Marks 2array* as a named structure and returns it.
This is used by 3make-array* when creating named structures.
You should not normally call it explicitly.
.end_defun

.defun named-structure-invoke operation structure &rest args
Invokes a named structure operation on 2structure*.
2operation* should be a keyword symbol, and 2structure* should be a
named structure.  The handler function of the named structure symbol,
found as the value of the 3named-structure-invoke* property of the
symbol, is called with appropriate arguments.

If the structure type has no 3named-structure-invoke* property,
3nil* is returned.  By convention, 3nil* is also returned
by the handler if it does not recognize 3operation*.

3(send 2structure* 2operation* 2args*...)*
has the same effect, by calling 3named-structure-invoke*.
.end_defun

See also the 3:named-structure-symbol* keyword to 3make-array*, (make-array-fun).

.section Common Lisp Defstruct
.setq cli-defstruct section-page

.defmac cli:defstruct
The version of 3defstruct* used in Common Lisp programs
differs from the traditional 3defstruct* in the defaults for
a few options and the meanings of a few of them.

The 3:conc-name* option defaults to the structure type name followed
by a hyphen in 3cli:defstruct*.  In traditional 3defstruct* it
defaults to 3nil*.

The 3:callable-constructors* option defaults to 3t* in
3cli:defstruct*, so that the constructor is a function.
Traditionally, it defaults to 3nil*.

The 3:alterant* option defaults to 3nil* in 3cli:defstruct*,
so that no alterant is defined.  Traditionally, an alterant is defined
by default with the name 3alter-2name**.

The 3:type* option defaults to 3:named-vector* in 3cli:defstruct*.
This makes a named structure, and you may specify how to print it.
The 3:predicate* option defaults to 3t* in this case.

If the 3:type* option is specified in 3cli:defstruct*, you never get
a named structure.  You get either a plain list or a plain vector.
There is no type-testing predicate, and you may not request one.
You may not say how to print the structure, either.

If you specify the 3:named* option along with 3:type*, you still
2do not* get a named structure.  You get a plain list or a plain vector
in which the structure name happens to be stored.  The type is either
3:named-list* or 3:phony-named-vector*.
The 3:predicate* option defaults to 3nil*, but you may specify 3t* yourself.
However, any randomly created list or vector with the structure name
stored in the right place will satisfy the predicate thus defined.
3typep* cannot recognize these phony named structures, and you
may not specify how to print them (they do not understand 3named-structure-invoke*.)
.end_defmac

.section The si:defstruct-description Structure
'setq defstruct-description page
	This section discusses the internal structures used by
3defstruct* that might be useful to programs that want to interface
to 3defstruct* nicely.  For example, if you want to write a program
that examines structures and displays them the way 3describe*
(see (describe-fun)) and the Inspector do, your program should work
by examining these structures.
The information in this section is also
necessary for anyone who is thinking of defining his own structure
types.

	Whenever the user defines a new structure using 3defstruct*,
3defstruct* creates an instance of the 3si:defstruct-description* structure.
This can be found as the 3si:defstruct-description* property of
the name of the structure; it contains such useful information as
the number of slots in the structure, the 3defstruct* options specified,
and so on.

This is a simplified version of the way 
the 3si:defstruct-description* structure is defined.
It omits some slots whose meaning is not worth documenting here.
(The actual definition is in the 3system-internals* package.)

.lisp
(defstruct (defstruct-description
		(:default-pointer description)
		(:conc-name defstruct-description-))
	   name
	   size
	   property-alist
	   slot-alist
	   documentation)
.end_lisp

	The 3name* slot contains the symbol supplied by the user to be
the name of his structure, such as 3spaceship* or
3phone-book-entry*.
	The 3size* slot contains the total number of slots in an
instance of this kind of structure.  This is 2not* the same number as
that obtained from the 3:size-symbol* or 3:size-macro* options to
3defstruct*.  A named structure, for example, usually uses up an extra
location to store the name of the structure, so the 3:size-macro* option
will get a number one larger than that stored in the 3defstruct*
description.
	The 3property-alist* slot contains an alist with pairs of the
form 3(2property-name* . 2property*)* containing properties placed there
by the 3:property* option to 3defstruct* or by property names used as
options to 3defstruct* (see the 3:property* option, (defstruct-property-option)).
	The 3slot-alist* slot contains an alist of pairs of the form
3(2slot-name* . 2slot-description*)*.  A 2slot-description* is
an instance of the 3defstruct-slot-description* structure.  The
3defstruct-slot-description* structure is defined something like
this (with other slots that are omitted here), also in the 3si* package:

.lisp
'setq defstruct-slot-description-structure page
(defstruct (defstruct-slot-description
		(:default-pointer slot-description)
		:conc-name)
  number
  ppss
  init-code
  type
  property-alist
  ref-macro-name
  documentation)
.end_lisp
	The 3number* slot contains the number of the location of this
slot in an instance of the structure.  Locations are numbered,
starting with 0, and continuing up to a number one less than the size of the
structure.  The actual location of the slot is determined by the
reference-consing function associated with the type of the structure; see
(defstruct-reference-consing-function).
	The 3ppss* slot contains the byte specifier code for this slot if
this slot is a byte field of its location.  If this slot is the entire
location, then the 3ppss* slot contains 3nil*.
	The 3init-code* slot contains the initialization code supplied
for this slot by the user in his 3defstruct* form.  If there is no
initialization code for this slot then the init-code slot contains a
canonical object which can be obtained (for comparison using 3eq*)
as the result of 3(si:defstruct-empty)*.
	The 3ref-macro-name* slot contains the symbol that is defined as
a macro or a subst that expands into a reference to this slot (that is, the name
of the accessor function).

.section Extensions to Defstruct
.setq defining-your-own-defstruct-types section-page

	The macro 3si:defstruct-define-type* can be used to teach 3defstruct*
about new types that it can use to implement structures.

.defmac si:defstruct-define-type
Is used for teaching 3defstruct* about new types.

The syntax of 3si:defstruct-define-type* is:
.lisp
(si:defstruct-define-type 2type*
  2option-1* 2option-2* 3...*)
.end_lisp
where each 2option* is either the symbolic name of an option or a list of
the form 3(2option-name* .  2rest*)*.  Different options interpret
2rest* in different ways.  The symbol 2type* is given an
3si:defstruct-type-description* property of a structure that describes the type
completely.

The semantics of 3si:defstruct-define-type* is the subject
of the rest of this section.
.end_defmac

.subsection Example

	Let us start by examining a sample call to
3defstruct-define-type*.  This is how the 3:list* type of structure might
have been defined:

.lisp
(si:defstruct-define-type :list
  (:cons (initialization-list description
                              keyword-options) 
         :list
         `(list . ,initialization-list))
  (:ref (slot-number description argument)
        `(nth ,slot-number ,argument)))
.end_lisp

This is the simplest possible form of 3defstruct-define-type*.  It
provides 3defstruct* with two Lisp forms: one for creating forms to
construct instances of the structure, and one for creating forms to
become the bodies of accessors for slots of the structure.

The keyword 3:cons* is followed by a list of three variables that will
be bound while the constructor-creating form is evaluated.  The first,
3initialization-list*, will be bound to a list of the initialization
forms for the slots of the structure.  The second, 3description*, will
be bound to the 3defstruct-description* structure for the structure
(see (defstruct-description)).  The third variable and the 3:list* keyword
will be explained later.

The keyword 3:ref* is followed by a list of three variables that will
be bound while the accessor-creating form is evaluated.  The first,
3slot-number*, will bound to the number of the slot that the new accessor
should reference.  The second, 3description*, will be bound to the
3defstruct-description* structure for the structure.  The third, 3argument*,
will be bound to the form that was provided as the argument to the accessor.

.subsection Options to si:defstruct-define-type

	This section is a catalog of all the options currently known
about by 3si:defstruct-define-type*.

.table 3
.item :cons
'kindex :cons si:defstruct-define-type
'setq defstruct-reference-consing-function page
Specifies the code to cons up a form that will construct an instance
of a structure of this type.

The 3:cons* option has the syntax:
.lisp
(:cons (2inits* 2description* 2keywords*) 2kind*
  2body*)
.end_lisp
2body* is some code that should construct and return a piece
of code that will construct, initialize, and return an instance of a
structure of this type.

The symbol 2inits* will be bound to the information that the
constructor conser should use to initialize the slots of the
structure.  The exact form of this argument is determined by the
symbol 2kind*.  There are currently two kinds of initialization.
There is the 3:list* kind, where 2inits* is bound to a list of
initializations, in the correct order, with 3nil*s in uninitialized
slots.  And there is the 3:alist* kind, where 2inits* is bound to an
alist with pairs of the form 3(2slot-number* . 2init-code*)*. Additional
kinds may be provided in the future.

The symbol 2description* will be bound to the instance of the
3defstruct-description* structure (see (defstruct-description)) that
3defstruct* maintains for this particular structure.  This is so that
the constructor conser can find out such things as the total size of
the structure it is supposed to create.

The symbol 2keywords* will be bound to an alist with pairs of the form
3(2keyword* . 2value*)*, where each 2keyword* was a keyword supplied to
the constructor that wasn't the name of a slot, and 2value* was the Lisp
object that followed the keyword.  This is how you can make your own special
keywords, like the existing 3:make-array* and 3:times* keywords.  See the
section on using the constructor, (using-defstruct-constructor).  You
specify the list of acceptable keywords with
the 3:cons-keywords* option (see (defstruct-define-type-keywords)).
	It is an error not to supply the 3:cons* option to
3si:defstruct-define-type*.

.item :ref
'kindex :ref si:defstruct-define-type
Specifies the code to cons up a form that will reference an
instance of a structure of this type.

The 3:ref* option has the syntax:
.lisp
(:ref (2number* 2description* 2arg-1* 3...* 2arg-n*)
  2body*)
.end_lisp
2body* is some code that should construct and return a piece
of code that will reference an instance of a structure of this type.

The symbol 2number* will be bound to the location of the slot
that is to be referenced.  This is the same number that is found
in the number slot of the 3defstruct-slot-description* structure
(see (defstruct-slot-description-structure)).

The symbol 2description* will be bound to the instance of the
3si:defstruct-description* structure that 3defstruct* maintains for this
particular structure.

The symbols 2arg-i* are bound to the forms supplied to the accessor as
arguments.  Normally there should be only one of these.  The 2last* argument
is the one that will be defaulted by the 3:default-pointer* option (see
(defstruct-default-pointer-option)).  3defstruct* will check that the user
has supplied exactly 2n* arguments to the accessor function before calling the
reference consing code.

It is an error not to supply the 3:ref* option to
3si:defstruct-define-type*. 

.item :overhead
'kindex :overhead si:defstruct-define-type
Declares to 3defstruct* that the implementation of this particular type
of structure ``uses up'' some number of locations in the object
actually constructed.  This option is used by various ``named'' types of
structures that store the name of the structure in one location.
For example, named arrays have an overhead of one, and named array
leaders an overhead of two, but named typed arrays have no overhead
since the structure type symbol is stored in the array leader whilst the
actual data specifying the values of the slots is stored in the array proper.

The syntax of 3:overhead* is 3(:overhead 2n*)*, where 2n* is a
fixnum that says how many locations of overhead this type needs.

This number is used only by the 3:size-macro* and 3:size-symbol*
options to 3defstruct* (see (defstruct-size-macro-option)).

.item :named
'kindex :named si:defstruct-define-type
Controls the use of the 3:named* option to 3defstruct*.  With no
argument, the 3:named* option means that this type is one which
records the structure type name somehow (not necessarily by using an
actual named structure).  With an argument, as in 3(:named
2type-name*)*, the symbol 2type-name* should be the name of some
other structure type that 3defstruct* should use if the user
specifies this type and 3:named* as well.  For example, the
definition of the 3:list* type contains 3(:named :named-list)*,
saying that a 3defstruct* that specifies 3(:list :named)* really
uses type 3:named-list*.

.item :cons-keywords
'kindex :cons-keywords si:defstruct-define-type
'setq defstruct-define-type-keywords page
Defines additional constructor keywords for this type of structure.
Using these keywords, one may specify additional information
about a structure at the time it is created (``consed'') using one of
its constructor functions or macros.
(The 3:times* constructor keyword for structures of type
3:grouped-array* is an example.)  The syntax is: 3(:cons-keywords
2keyword-1* ... 2keyword-n*)* where each 2keyword* is a symbol
that the constructor conser expects to find in the 2keywords* alist
(explained above).

.xitem :keywords si:defstruct-define-type
3:keywords* is an old name for the 3:cons-keywords* option.

.item :defstruct-keywords
'kindex :defstruct-keywords si:defstruct-define-type
Defines additional 3defstruct* options allowed for this
type of structure.  (The 3:subtype* option for structures of type
3:array* is an example.)  These options take effect at the time
the structure is defined using 3defstruct*, and thus affect
all structures of a particular type (unless overridden in some way.)
In contrast, the 3:cons-keywords* options affect the creation
of individual structures of a particular type.

The syntax is:
3(:defstruct-keywords 2keyword-1* ... 2keyword-n*)* where each
2keyword* is a keyword that 3defstruct* will recognize as an option.
3defstruct* puts such options, with their values, in the
3property-alist* slot of the 3defstuct-description* structure
(defined above)

.xitem :predicate si:defstruct-define-type
Tells 3defstruct* how to produce predicates for a particular type
(for the 3:predicate* option to 3defstruct*).  Its syntax is:
.lisp
(:predicate (2description* 2name*)
  2body*3...*)
.end_lisp
The variable 2description* is bound to the 3defstruct-description*
structure maintained for the structure for which a predicate is being
generated.  The variable 2name* is bound to the symbol that is to be
defined as a predicate.  2body* is a piece of code to compute
the defining form for the predicate.  A typical use of this
option might look like:
.lisp
(:predicate (description name)
  `(defun ,name (x)
     (and (frobbozp x)
	  (eq (frobbozref x 0)
	      ',(si:defstruct-description-name
		  description)))))
.end_lisp

.xitem :copier si:defstruct-define-type
3defstruct* knows how to generate a copier function using the
constructor and reference code that must be provided with any new
defstruct type.  Nevertheless it is sometimes desirable to specify a
specific method of copying a particular defstruct type.  The 3:copier*
option to 3si:defstruct-define-type* allow this to be done:
.lisp
(:copier (2description* 2name*)
  2body*)
.end_lisp
As with the 3:predicate* option, 2description* is bound to an instance of the
3defstruct-description* structure, 2name* is bound to the symbol to be defined,
and 2body* is some code to evaluate to get the defining form.  For example:
.lisp
(:copier (description name)
  `(fdefine ',name 'copy-frobboz))
.end_lisp

.item :defstruct
'kindex :defstruct si:defstruct-define-type
The 3:defstruct* option to 3si:defstruct-define-type* allows the user
to run some code and return some forms as part of the expansion of the
3defstruct* macro.

The 3:defstruct* option has the syntax:
.lisp
(:defstruct (2description*)
  2body*)
.end_lisp
2body* is a piece of code that will be run whenever 3defstruct*
is expanding a 3defstruct* form that defines a structure of this type.
The symbol 2description* will be bound to the instance of the
3defstruct-description* structure that 3defstruct* maintains for this
particular structure.

The value returned by the 2body* should be a 2list*
of forms to be included with those that the 3defstruct* expands into.
Thus, if you only want to run some code at 3defstruct*-expand time, and
you don't want to actually output any additional code, then you should
be careful to return 3nil* from the code in this option.

3defstruct* will cause the 2body* forms to be evaluated as early
as possible in the parsing of a structure definition, and cause the
returned forms to be evalutated as late as possible in the
macro-expansion of the 3defstuct* forms. This is so that 2body*
can rehack arguments, signal errors, and the like before many of
3defstruct*'s internal forms are executed, while enabling it to
return code which will modify or extend the default forms produced by
a vanilla 3defstruct*.

.end_table
